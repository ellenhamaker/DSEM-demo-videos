TITLE: Multilevel DSEM model 6 with TINTERVAL

       Within level:
       - no parameters at this level

       Between level:
       - two random means, four random slopes, and two random (log) variances
       - all random effects are regressed on an observed predictor (which is grand mean centered)
       - an observed outcome is regressed on all random effects and observed predictor 

DATA:
    file = MLdata.dat;
    
VARIABLE:
    NAMES ARE CL TE N RS time person; 
    USEVARIABLES = CL TE N RS;
    BETWEEN = N RS;
    CLUSTER = person;
    LAGGED = CL(1) TE(1);
    TINTERVAL = time(1);

DEFINE:	
    CENTER N (GRANDMEAN);

ANALYSIS:   
    TYPE = TWOLEVEL RANDOM;
    ESTIMATOR = BAYES;
    PROC = 4;
    BITER = (5000); 

MODEL: 
    %WITHIN%
    phiCL | CL ON CL&1;
    betaCL | CL ON TE;
    phiTE | TE ON TE&1;
    betaTE | TE ON CL&1;
    logVCL | CL;
    logVTE | TE;

    %BETWEEN%
    CL TE phiCL-logVTE ON N;
    RS ON CL TE phiCL-logVTE N;

OUTPUT:
    TECH1 TECH8 STDYX;

PLOT:
    TYPE = PLOT3;
    FACTOR = ALL(100);

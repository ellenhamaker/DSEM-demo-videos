TITLE: Multilevel DSEM model 6 with TINTERVAL

       Within level:
       - no parameters at this level

       Between level:
       - two random means, four random slopes, and two random (log) variances
       - all random effects are regressed on an observed predictor (which is grand mean centered)
       - an observed outcome is regressed on all random effects and observed predictor 

DATA:
    file = MLdata.dat;                  ! data file
    
VARIABLE:
    NAMES ARE CL TE N RS time person;   ! variables names (in the order they appear in the data file) 
    USEVARIABLES = CL TE N RS;          ! which variables to include in the analysis
    BETWEEN = N RS;                     ! which variables only have between-person variance (i.e., a constant within person)
    CLUSTER = person;                   ! which variable indicates the clustering of the data
    LAGGED = CL(1) TE(1);               ! create lagged versions of CL and TE (lag 1)
    TINTERVAL = time(1);                ! which variable indicates the timing of observations; obtain results for an interval of 1 on this timescale

DEFINE:	
    CENTER N (GRANDMEAN);               ! use grand mean centering for the between level variable

ANALYSIS:   
    TYPE = TWOLEVEL RANDOM;             ! two-level data and allow for random slopes and/or random residual variances
    ESTIMATOR = BAYES;                  ! use Bayesian estimation
    PROC = 2;                           ! use 2 processors
    BITER = (5000);                     ! run at least 5000 iterations (more if needed according to PSR)

MODEL: 
    %WITHIN%
    phiCL | CL ON CL&1;                 ! random autoregression for CL
    betaCL | CL ON TE;                  ! random cross-regression from TE_t -> CL_t
    phiTE | TE ON TE&1;                 ! random autoregression for TE
    betaTE | TE ON CL&1;                ! random cross-lagged regression from CL_t-1 -> TE_t
    logVCL | CL;                        ! random residual variance for CL
    logVTE | TE;                        ! random residual variance for TE

    %BETWEEN%
    CL TE phiCL-logVTE ON N;            ! regress all 8 random effects (2 means, 4 slopes, 2 log residual variances) on the between level predictor
    RS ON CL TE phiCL-logVTE N;         ! regress between level outcome on all 8 random effects and on the between level predictor

OUTPUT:
    TECH1 TECH8 STDYX;                  ! obtain additional output

PLOT:
    TYPE = PLOT3;                       ! enable plot options 
    FACTOR = ALL(100);                  ! sample factor scores (for the random effects) per person